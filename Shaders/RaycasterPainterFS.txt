#extension GL_ARB_texture_rectangle : enable
#ifdef GL_EXT_gpu_shader4
#extension GL_EXT_gpu_shader4 : enable
#endif

#define NQVTK_RAYCASTER_VOLUMECOUNT 4

uniform sampler2DRect infoPrevious;
uniform sampler2DRect infoCurrent;
uniform int layer;
uniform float viewportX;
uniform float viewportY;

uniform sampler3D volume[NQVTK_RAYCASTER_VOLUMECOUNT];
uniform float volumeDataShift[NQVTK_RAYCASTER_VOLUMECOUNT];
uniform float volumeDataScale[NQVTK_RAYCASTER_VOLUMECOUNT];
uniform vec3 volumeOrigin[NQVTK_RAYCASTER_VOLUMECOUNT];
uniform vec3 volumeSize[NQVTK_RAYCASTER_VOLUMECOUNT];

uniform float tfStart[NQVTK_RAYCASTER_VOLUMECOUNT];
uniform float tfEnd[NQVTK_RAYCASTER_VOLUMECOUNT];

uniform float stepSize;

uniform float testParam;

// Prototypes from LibUtility
bool getBit(float byte, int bit);
vec3 phongShading(vec3 matColor, vec3 normal);

// Get vector from volume
vec3 getVector(int v, vec3 pos) {
	vec4 tpos = gl_TextureMatrixInverse[v] * vec4(pos, 1.0);
	vec3 p = ((tpos.xyz / tpos.w) - volumeOrigin[v]) / volumeSize[v];
	return volumeDataShift[v] + volumeDataScale[v] * texture3D(volume[v], p).xyz;
}

// Compute Jacobian of vector volume
mat3 computeJacobian(int v, vec3 pos, float delta, vec3 val) {
#ifdef NQVTK_RAYCASTER_CENTRALDIFFERENCES
	vec3 dx = (getVector(v, pos + vec3(0.5 * delta, 0.0, 0.0)) - 
		getVector(v, pos - vec3(0.5 * delta, 0.0, 0.0))) / delta;
	vec3 dy = (getVector(v, pos + vec3(0.0, 0.5 * delta, 0.0)) - 
		getVector(v, pos - vec3(0.0, 0.5 * delta, 0.0))) / delta;
	vec3 dz = (getVector(v, pos + vec3(0.0, 0.0, 0.5 * delta)) - 
		getVector(v, pos - vec3(0.0, 0.0, 0.5 * delta))) / delta;
#else
	vec3 dx = (getVector(v, pos + vec3(delta, 0.0, 0.0)) - val) / delta;
	vec3 dy = (getVector(v, pos + vec3(0.0, delta, 0.0)) - val) / delta;
	vec3 dz = (getVector(v, pos + vec3(0.0, 0.0, delta)) - val) / delta;
#endif
	// Add identity (because we should have used pos + vec instead of vec)
	return mat3(dx, dy, dz) + mat3(1.0);
}

// Compute determinant of matrix
float determinant(mat3 m) {
	return m[0][0] * m[1][1] * m[2][2] 
		+ m[0][1] * m[1][2] * m[2][0] 
		+ m[0][2] * m[1][0] * m[2][1] 
		- m[0][2] * m[1][1] * m[2][0] 
		- m[0][1] * m[1][0] * m[2][2] 
		- m[0][0] * m[1][2] * m[2][1];
}

// Get color, value from volume
vec4 sampleVolume(int v, vec3 pos) {
	float val;
	vec3 color;
#ifdef NQVTK_RAYCASTER_VECTORFIELD
	if (v == 0) {
		// Vector volume
		vec3 vec = getVector(v, pos);
		float deformation = length(vec / 
			(volumeDataScale[v] + volumeDataShift[v]));
		mat3 jac = computeJacobian(v, pos, stepSize, vec);
		float divergence = jac[0][0] + jac[1][1] + jac[2][2];
		float detJ = 1.0 / abs(determinant(jac));
		float growth = detJ - 1.0;
		float shrinkage = max(0.0, -growth);
		growth = max(0.0, growth);
		float convergence = max(0.0, -divergence);
		divergence = max(0.0, divergence);
		float tot = //deformation + 
			//divergence + convergence + 
			max(growth + shrinkage, 0.001);
		color = //deformation * vec3(0.7) +
			//divergence * vec3(1.0, 0.5, 0.0) + 
			//convergence * vec3(0.0, 0.5, 1.0) + 
			growth * vec3(0.0, 1.0, 0.0) + 
			shrinkage * vec3(1.0, 0.0, 0.0);
		color /= tot;
		val = //max(deformation, 
			//max(divergence, convergence));
			max(growth, shrinkage);
	} else {
#endif
#ifdef NQVTK_RAYCASTER_DEFORM
		// TODO: adjust for the transformation of the vector field
		pos += (testParam - 0.5) * 4.0 * getVector(0, pos);
#endif
		vec4 tpos = gl_TextureMatrixInverse[v] * vec4(pos, 1.0);
		vec3 p = ((tpos.xyz / tpos.w) - volumeOrigin[v]) / volumeSize[v];
		val = texture3D(volume[v], p).x;
		// TODO: adjust val by volumeData{Shift,Scale}[v]
		color = vec3(1.0) * val;
#ifdef NQVTK_RAYCASTER_VECTORFIELD
	}
#endif
	return vec4(color, val);
}

// Get value from volume
float getValue(int v, vec3 pos) {
	return sampleVolume(v, pos).a;
}

// Compute gradient of the volume
vec3 computeGradient(int v, vec3 pos, float delta, float val) {
#ifdef NQVTK_RAYCASTER_CENTRALDIFFERENCES
	float dx = (getValue(v, pos + vec3(0.5 * delta, 0.0, 0.0)) - 
		getValue(v, pos - vec3(0.5 * delta, 0.0, 0.0))) / delta;
	float dy = (getValue(v, pos + vec3(0.0, 0.5 * delta, 0.0)) - 
		getValue(v, pos - vec3(0.0, 0.5 * delta, 0.0))) / delta;
	float dz = (getValue(v, pos + vec3(0.0, 0.0, 0.5 * delta)) - 
		getValue(v, pos - vec3(0.0, 0.0, 0.5 * delta))) / delta;
#else
	float dx = (getValue(v, pos + vec3(delta, 0.0, 0.0)) - val) / delta;
	float dy = (getValue(v, pos + vec3(0.0, delta, 0.0)) - val) / delta;
	float dz = (getValue(v, pos + vec3(0.0, 0.0, delta)) - val) / delta;
#endif
	return vec3(dx, dy, dz);
}

// Adjust opacity for step length
float adjustOpacity(float val, float stepLength) {
	return 1.0 - pow(1.0 - val, stepLength);
}

// Main shader function
void main() {
	// Get info
	vec4 r0 = gl_FragCoord;
	r0.x -= viewportX;
	r0.y -= viewportY;
	vec4 infoBefore = texture2DRect(infoPrevious, r0.xy);
	vec4 infoAfter = texture2DRect(infoCurrent, r0.xy);
	if (layer == 0) discard;

	// Discard background
	if (length(infoAfter) == 0.0) discard;

	// Raycast this slab
	vec3 startPos = infoBefore.rgb;
	vec3 endPos = infoAfter.rgb;
	vec3 ray = endPos - startPos;
	// Determine step, number of steps
	vec3 step = normalize(ray) * stepSize;
	float stepLength = stepSize;
	int numSteps = int(ceil(length(ray) / stepSize));
	// Start tracing
	vec4 accum = vec4(vec3(0.0), 1.0);
	vec3 pos = startPos;
	// For each step along the ray...
	for (int i = 0; i < numSteps + 1; ++i) {
		vec4 sample[NQVTK_RAYCASTER_VOLUMECOUNT];
		for (int v = 0; v < NQVTK_RAYCASTER_VOLUMECOUNT; ++v) {
			// If the volume is active, sample it
			if (getBit(infoBefore.a, v)) {
				sample[v] = sampleVolume(v, pos);
			} else {
				sample[v] = vec4(0.0);
			}
		}

		// Combine samples into final value / color
		vec4 combined = vec4(vec3(0.0), 1.0);
		float smoothOpacity = 1.0;
		// TODO: make combiner more flexible
		for (int v = 0; v < NQVTK_RAYCASTER_VOLUMECOUNT; ++v) {
			// Apply the volume's transfer function
			// TODO: implement real transfer function support, remove this hack
			float tfSize = max(tfEnd[v] - tfStart[v], 0.00001);
			float op = clamp((sample[v].a - tfStart[v]) / tfSize, 0.0, 1.0);
			// Compute correct opacity for blending
			op = adjustOpacity(op, stepLength);

			// TODO: apply smooth version of transfer function to sample.a for gradients
			smoothOpacity = (1.0 - sample[v].a) * smoothOpacity;

			// Accumulate samples
			combined = vec4(
				combined.a * sample[v].rgb * op + combined.rgb, 
				(1.0 - op) * combined.a);
		}

		vec3 color = combined.rgb;
		float opacity = 1.0 - combined.a;
		smoothOpacity = 1.0 - smoothOpacity;

#ifdef NQVTK_RAYCASTER_LIGHTING
		// Compute gradient of accumulated smooth opacity volume
		// TODO: refactor code above to implement this function
		// Or can we just light samples first, then combine?
		gradient = computeCombinedGradient(pos, stepSize, smoothOpacity);
#endif

		/* Volume IBIS Test
		// TODO: move to separate shader?
		vec4 col = vec4(0.0);
		if (getBit(infoBefore.a, 0) && getBit(infoBefore.a, 1)) {
			float v0 = getValue(0, pos);
			float v1 = getValue(1, pos);
			vec4 col0 = vec4(vec3(1.0, 0.9, 0.4), mapValue(v0, stepLength).a);
			vec4 col1 = vec4(vec3(0.3, 0.6, 1.0), mapValue(v1, stepLength).a);
			vec3 gmin, gmax;
			vec4 colmin, colmax;
			if (v0 > v1) {
				colmin = col1; colmax = col0;
				gmin = computeGradient(1, pos, stepSize, v1);
				gmax = computeGradient(0, pos, stepSize, v0);
			} else {
				colmin = col0; colmax = col1;
				gmin = computeGradient(0, pos, stepSize, v0);
				gmax = computeGradient(1, pos, stepSize, v1);
			}
			// Adjust outer
			colmax.a = colmax.a / 30.0;
			if (colmax.a > colmin.a) {
				col = colmax;
				g = gmax;
			} else {
				col = colmin;
				g = gmin;
			}
		} else if (getBit(infoBefore.a, 0)) {
			float v0 = getValue(0, pos);
			col = vec4(vec3(1.0, 0.9, 0.4), mapValue(v0, stepLength).a);
			// Adjust outer
			col.a = col.a / 30.0;
			g = computeGradient(0, pos, stepSize, v0);
		} else if (getBit(infoBefore.a, 1)) {
			float v1 = getValue(1, pos);
			col = vec4(vec3(0.3, 0.6, 1.0), mapValue(v1, stepLength).a);
			// Adjust outer
			col.a = col.a / 30.0;
			g = computeGradient(1, pos, stepSize, v1);
		}
		//*/

#ifdef NQVTK_RAYCASTER_LIGHTING
		// Lighting (only if we have a normal)
		// TODO: normals should take slab boundaries into account
		//       ...but only in some cases (clipping planes!)
		if (length(gradient) > 0.0) {
			vec3 normal = normalize(gl_NormalMatrix * -gradient);
			color = phongShading(color, normal);
		}
#endif

		// Accumulate colors and opacities
		accum = vec4(accum.a * color * opacity + accum.rgb, (1.0 - opacity) * accum.a);

		// If alpha is (almost) saturated, terminate the ray
		if (accum.a < 0.001) i = numSteps + 1;
		// The last step should stay within the slab
		if (i == numSteps) {
			step = endPos - pos;
			stepLength = length(step);
		}
		// Step along the ray
		pos += step;
	}

	// Colors are already pre-multiplied with alpha for slab compositing
	gl_FragColor = vec4(accum.rgb, 1.0 - accum.a);
}
