#extension GL_ARB_texture_rectangle : enable
#extension GL_ARB_draw_buffers : enable
#ifdef GL_EXT_gpu_shader4
#extension GL_EXT_gpu_shader4 : enable
#endif

uniform sampler2DRectShadow depthBuffer;
uniform sampler2DRect infoBuffer;
uniform int layer;
uniform int objectId;
varying vec3 normal;
varying vec4 color;
varying float depthInCamera;

// Prototypes from LibUtility
float setBit(float byte, int bit, bool on);
bool getBit(float byte, int bit);

// Shader main function
void main() {
	vec4 r0 = gl_FragCoord;
	vec4 col = color;

	// Depth peeling
	if (layer > 0) {
		float r1 = shadow2DRect(depthBuffer, r0.xyz).x;
		r1 = r1 - 0.5;
		if (r1 < 0.0) { discard; }
	}

	// Get the previous info buffer
	vec4 prevInfo = vec4(0.0);
	if (layer > 0) {
		prevInfo = texture2DRect(infoBuffer, r0.xy);
	}

	// Coplanarity peeling
	float inOutMask = prevInfo.x;
	if (getBit(inOutMask, objectId) == gl_FrontFacing) {
		discard;
	}

	// Encode in-out mask
	if (objectId >= 0) {
		inOutMask = setBit(inOutMask, objectId, gl_FrontFacing);
	}
	// Store data
	gl_FragData[0] = vec4(inOutMask, depthInCamera, gl_TexCoord[0].xy);
}
